/*
 * Copyright (C) 2018 Knot.x Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'maven'
}

allprojects {
  repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://plugins.gradle.org/m2/' }
    maven { url "https://oss.sonatype.org/content/groups/staging/" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
  }
}

defaultTasks = ['build']

subprojects { subproject ->
  group = 'io.knotx'

  apply plugin: 'java-library'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:deprecation'
    options.incremental = true
  }

  dependencies {
    implementation platform("io.knotx:knotx-dependencies:${project.version}")
    api 'io.knotx:knotx-core'

    implementation 'io.vertx:vertx-core'
    implementation 'io.vertx:vertx-service-proxy'
    implementation 'io.vertx:vertx-rx-java2'

    annotationProcessor group: "io.vertx", name: "vertx-codegen", version: "3.9.0", classifier: "processor"

    implementation 'io.vertx:vertx-codegen'

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.junit.jupiter:junit-jupiter-migrationsupport'
    testImplementation 'org.junit.vintage:junit-vintage-engine'
    testImplementation 'uk.co.datumedge:hamcrest-json'
    testImplementation 'org.hamcrest:hamcrest-all'
    testImplementation 'com.github.tomakehurst:wiremock'

    testImplementation 'io.vertx:vertx-config'
    testImplementation 'io.vertx:vertx-junit5'
    testImplementation 'io.knotx:knotx-mocks'
    testImplementation 'io.knotx:knotx-junit5'
  }

  sourceSets {
    main {
      java {
        srcDirs += 'src/main/generated'
      }
    }
    test {
      resources {
        srcDirs += 'src/test/resources'
      }
    }
  }

  clean.doFirst {
    file('src/main/generated').deleteDir()
  }

  task annotationProcessing(type: JavaCompile, group: 'build') { // codegen
    source = sourceSets.main.java
    options.annotationProcessorPath = configurations.annotationProcessor
    classpath = configurations.compileClasspath + configurations.runtimeClasspath + fileTree('src/main/resources')
    destinationDir = project.file('src/main/generated')
    options.compilerArgs = [
        "-proc:only",
        "-processor", "io.vertx.codegen.CodeGenProcessor",
        "-Acodegen.output=${destinationDir.absolutePath}"
    ]
  }

  task annotationProcessingCleanup() {
    doLast {
      delete "${project.projectDir}/src/main/generated"
      println "Removing: ${project.projectDir}/src/main/generated"
    }
  }

  annotationProcessing.dependsOn annotationProcessingCleanup

  test {
    useJUnitPlatform()

    include 'io/knotx/te/**'

    systemProperties = [
        'vertx.logger-delegate-factory-class-name': 'io.vertx.core.logging.SLF4JLogDelegateFactory'
    ]
  }

  if (subproject.name != 'knotx-template-engine-it-test') {
    apply from: "${rootProject.projectDir}/release.gradle"
  }
}
